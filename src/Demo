	include	"/includes/hardware/offsets.i"
	include	"/includes/exec/exec_lib.i"
	include	"/includes/graphics/graphics_lib.i"
	include	"/includes/libraries/dos_lib.i"
	include	"/includes/libraries/dos.i"
StructLength	equ	32
Name	equ	0
Length	equ	4
Pointer	equ	8
Width	equ	12
Hight	equ	14
Offset	equ	16
Tempo	equ	20
BlitterInfo	equ	24
HorLine	equ	28
	clr.l	ObjPointer1
	move.l	4.w,a6
	lea	grafname,a1
	jsr	OpenLibrary(a6)
	move.l	d0,_GfxBase
	lea	dosname,a1
	moveq	#0,d0
	jsr	OpenLibrary(a6)
	move.l	d0,_DosBase
	move.l	#516,d0
	moveq	#3,d1
	jsr	AllocMem(a6)
	move.l	d0,CL
	move.l	#516,d0
	moveq	#3,d1
	jsr	AllocMem(a6)
	move.l	d0,CL2
	lea	BitPlanes1,a3
	moveq	#9,d7
Allocer	move.l	#9204,d0
	move.l	#$10003,d1
	jsr	AllocMem(a6)
	move.l	d0,(a3)+
	dbf	d7,Allocer
	lea	ObjInfo,a5
	move.w	(a5)+,d5
	sub.w	#1,d5
GraphicLoad	move.l	Name(a5),d1
	move.l	#-2,d2
	move.l	_DosBase,a6
	jsr	Lock(a6)
	move.l	d0,d7
	tst.l	d0
	beq	FileNotFound
	move.l	#260,d0
	move.l	#1,d1
	move.l	4.w,a6
	jsr	AllocMem(a6)
	move.l	d0,IFFData
	move.l	d7,d1
	move.l	IFFData,d2
	move.l	d2,d6
	move.l	_DosBase,a6
	jsr	Examine(a6)
	move.l	d6,a6
	move.l	124(a6),DateiLength
	move.l	IFFData,a1
	move.l	#260,d0
	move.l	4.w,a6
	jsr	FreeMem(a6)
	move.l	DateiLength,d0
	move.l	#1,d1
	jsr	AllocMem(a6)
	move.l	d0,IFFData
	move.l	Name(a5),d1
	move.l	#1005,d2
	move.l	_DosBase,a6
	jsr	Open(a6)
	move.l	d0,d7
	move.l	d7,d1
	move.l	IFFData,d2
	move.l	DateiLength,d3
	jsr	Read(a6)
	move.l	d7,d1
	jsr	Close(a6)
	move.l	IFFData,a0
	move.w	20(a0),d0
	move.w	d0,Breite
	lsr.w	#3,d0
	move.w	d0,BytesPerRow
	move.w	22(a0),Hohe
	move.b	28(a0),Depth+1
	move.b	30(a0),Compression
	move.w	Breite,d0
	lsr.w	#3,d0
	add.w	#2,d0
	mulu	Hohe,d0
	move.l	d0,Length(a5)
	move.l	Pointer(a5),a3
	moveq	#5,d7
	move.l	4.w,a6
ObjAllocer	move.l	Length(a5),d0
	move.l	#$10003,d1
	jsr	AllocMem(a6)
	move.l	d0,(a3)+
	dbf	d7,ObjAllocer
	move.w	Hohe,Hight(a5)
	move.w	Breite,Width(a5)
	clr.l	Empty
	lea	Empty,a0
	move.l	IFFData,a2
CMAPLoop	move.b	1(a0),(a0)
	move.b	2(a0),1(a0)
	move.b	3(a0),2(a0)
	move.b	(a2)+,3(a0)
	cmpi.l	#"CMAP",Empty
	bne	CMAPLoop
	adda.l	#4,a2
	move.w	Depth,d0
	move.w	#1,d1
	sub.w	#1,d0
Farben	lsl.w	d1
	dbf	d0,Farben
	sub.w	#1,d1
	lea	ColorMap,a0
FarbenLoad	move.b	(a2)+,d2
	move.b	(a2)+,d3
	move.b	(a2)+,d4
	lsr.b	#4,d2
	move.b	d2,(a0)+
	lsr.b	#4,d4
	or.b	d3,d4
	move.b	d4,(a0)+
	dbf	d1,FarbenLoad
	move.l	IFFData,a2
	clr.l	Empty
	lea	Empty,a0
BODYLoop	move.b	1(a0),(a0)
	move.b	2(a0),1(a0)
	move.b	3(a0),2(a0)
	move.b	(a2)+,3(a0)
	cmpi.l	#"BODY",Empty
	bne	BODYLoop
	adda.l	#4,a2
	tst.b	Compression
	bne	DifficultCode
	moveq	#0,d3
EasyBit	move.w	Hohe,d2
	sub.w	#1,d2
EasyDepth	move.l	Pointer(a5),a0
	move.w	Depth,d1
	sub.w	#1,d1
EasyRow	move.l	(a0)+,a1
	add.l	d3,a1
	move.w	BytesPerRow,d0
	sub.w	#1,d0
	move.b	1(a2),d7
	swap	d7
	move.b	(a2),d7
EasyCopy	move.b	(a2)+,(a1)+
	dbf	d0,EasyCopy
	move.b	d7,(a1)+
	swap	d7
	move.b	d7,(a1)+
	dbf	d1,EasyRow
	add.l	BytesRowLong,d3
	add.l	#2,d3
	dbf	d2,EasyDepth
	bra	DeCodeEnd
DifficultCode	clr.l	xyPos
	move.w	Hohe,d3
	sub.w	#1,d3
HightLoop	move.l	Pointer(a5),a1
	move.w	Depth,d2
	sub.w	#1,d2
DepthLoop	move.l	(a1)+,d0
	add.l	xyPos,d0
	move.l	d0,a0
	moveq	#0,d1
Row	cmp.w	BytesPerRow,d1
	bge	Interpreted
	move.b	(a2)+,Byte
	move.b	(a2)+,Byte+1
	moveq	#0,d0
	move.b	Byte,d0
	cmpi.b	#127,d0
	bls	DeEasy
DeSame	sub.b	#3,d0
	move.w	#254,d4
	sub.b	d0,d4
	add.w	d4,d1
	sub.w	#1,d4
DeSameCopy	move.b	Byte+1,(a0)+
	dbf	d4,DeSameCopy
	bra	Row
DeEasy	move.b	Byte+1,(a0)+
	add.w	#1,d1
	tst.b	Byte
	beq	Row
	add.w	d0,d1
	sub.w	#1,d0
DeEasyCopy	move.b	(a2)+,(a0)+
	dbf	d0,DeEasyCopy
	bra	Row
Interpreted	move.w	BytesPerRow,d0
	neg.w	d0
	move.b	(a0,d0.w),(a0)+
	move.b	(a0,d0.w),(a0)
	dbf	d2,DepthLoop
	move.l	BytesRowLong,d0
	add.l	d0,xyPos
	add.l	#2,xyPos
	dbf	d3,HightLoop
DeCodeEnd	move.l	IFFData,a1
	move.l	DateiLength,d0
	jsr	FreeMem(a6)
	lea	$DFF000,a1
	moveq	#1,d7
	move.l	Pointer(a5),a3
	move.l	a3,a4
	adda.l	#20,a4
Umrissmaker	btst	#6,dmaconr(a1)
	bne	Umrissmaker
	move.w	#%0000111111111110,bltcon0(a1)
	move.w	#%0000000000000000,bltcon1(a1)
	move.w	#$FFFF,bltafwm(a1)
	move.w	#$FFFF,bltalwm(a1)
	move.l	(a3)+,bltapth(a1)
	move.l	(a3)+,bltbpth(a1)
	move.l	(a3)+,bltcpth(a1)
	move.l	(a4),bltdpth(a1)
	clr.w	bltamod(a1)
	clr.w	bltbmod(a1)
	clr.w	bltcmod(a1)
	clr.w	bltdmod(a1)
	move.w	Hohe,d0
	lsl.w	#6,d0
	move.w	Breite,d1
	lsr.w	#4,d1
	add.w	#1,d1
	add.w	d1,d0
	move.w	d0,bltsize(a1)
	dbf	d7,Umrissmaker
	adda.l	#StructLength,a5
	dbf	d5,GraphicLoad
	lea	$DFF000,a5
	move.l	CL,a4
	move.l	CL2,a2
	move.l	BitMap1,a3
	move.l	BitMap2,a1
	move.w	#$E0,d0
	moveq	#4,d7
CopperBplInit	move.w	d0,(a4)+
	move.w	d0,(a2)+
	add.w	#2,d0
	move.w	(a3)+,(a4)+
	move.w	(a1)+,(a2)+
	move.w	d0,(a4)+
	move.w	d0,(a2)+
	add.w	#2,d0
	move.w	(a3)+,(a4)+
	move.w	(a1)+,(a2)+
	dbf	d7,CopperBplInit
	lea	Color01Smoove,a0
	move.l	#104*$1000000+$1FFFE,d1
	moveq	#58,d7
CopperColInit	move.l	d1,(a2)+
	move.l	d1,(a4)+
	move.w	#color01,(a2)+
	move.w	#color01,(a4)+
	move.w	(a0),(a2)+
	move.w	(a0)+,(a4)+
	cmpi.w	#5,d7
	bne	NoJump
	add.l	#$10000000,d1
NoJump	add.l	#$1000000,d1
	dbf	d7,CopperColInit
	move.l	#$FFFFFFFE,(a2)
	move.l	#$FFFFFFFE,(a4)
BlitterFinito	btst	#6,dmaconr(a5)
	bne	BlitterFinito
	move.w	#$1E0,dmacon(a5)
	move.w	#0,spr0data(a5)
	move.w	#0,spr0datb(a5)
	lea	ColorMap,a3
	move.w	#15,d7
	lea	$DFF180,a2
ColorInit	move.l	(a3)+,(a2)+
	dbf	d7,ColorInit
	move.w	#70*256+96,diwstrt(a5)
	move.w	#(270-256)*256+458-256,diwstop(a5)
	move.w	#40,ddfstrt(a5)
	move.w	#214,ddfstop(a5)
	move.l	BitPlanes1,bpl1pth(a5)
	move.l	BitPlanes1,bpl2pth(a5)
	move.l	BitPlanes1,bpl3pth(a5)
	move.l	BitPlanes1,bpl4pth(a5)
	move.l	BitPlanes1,bpl5pth(a5)
	move.w	#0,bplcon1(a5)
	move.w	#0,bplcon2(a5)
	move.w	#$5000,bplcon0(a5)
	move.w	#0,bpl1mod(a5)
	move.w	#0,bpl2mod(a5)
	move.w	#0,bplcon2(a5)
	move.l	CL,cop1lch(a5)
	move.w	#0,copjmp1(a5)
	move.w	#$85C0,dmacon(a5)
	move.l	#0,ObjOffset1
	move.l	#0,ObjOffset2
Beweg	move.l	CL2,cop1lch(a5)
	bsr	SideChill
	bsr	ColorCycle
	move.w	#1800,d0
Zoff	dbf	d0,Zoff
MouseCheck	btst	#6,ciaapra
	beq	BlitterFinito2
	move.l	BitMap2,d0
	move.l	BitMap1,BitMap2
	move.l	d0,BitMap1
	move.l	CL,d0
	move.l	CL2,CL
	move.l	d0,CL2
	lea	ObjInfo,a1
	move.w	(a1)+,d5
	sub.w	#1,d5
ObjDraw	move.w	Width(a1),d0
	lsr.w	#4,d0
	move.l	BlitterInfo(a1),a0
	adda.l	#2,a0
	move.l	Offset(a1),d1
	lsr.w	#5,d1
	and.l	#$FFFF,d1
	moveq	#0,d7
	moveq	#0,d6
Easyier	cmp.w	d0,d1
	blt	AnalyserLoop
	sub.w	d0,d1
	bra.s	Easyier
AnalyserLoop	add.w	#1,d7
	move.w	d6,(a0)+
	move.w	d0,d2
	sub.w	d1,d2
	add.w	d2,d6
	moveq	#1,d3
	cmpi.w	#23,d6
	ble	KeinUeberlauf
	sub.w	#23,d6
	sub.w	d6,d2
	move.w	#23,d6
	moveq	#0,d3
KeinUeberlauf	move.w	d2,(a0)+
	move.l	d1,(a0)+
	move.w	d3,(a0)+
	moveq	#0,d1
	cmpi.w	#23,d6
	bne	AnalyserLoop
	sub.w	#1,d7
	move.l	BlitterInfo(a1),a0
	move.w	d7,(a0)
	move.l	BitMap2,a4
	move.l	Pointer(a1),a3
	moveq	#4,d7
Abschnitt	move.l	BlitterInfo(a1),a0
	move.w	(a0)+,d6
Abbild	lea	BlitSpeicher,a2
	move.l	Offset(a1),d0
	lsr.w	d0
	and.w	#$F,d0
	move.w	#$FFFF,d1
	lsr.w	d0,d1
	lsr.w	d1
	not.b	d0
	and.l	#$F,d0
	move.w	#$FFFF,d2
	lsl.w	d0,d2
	swap	d0
	lsr.l	#4,d0
	move.w	d0,10(a2)
	or.w	#%0000111111001010,d0
	move.w	d0,8(a2)
	move.w	d1,12(a2)
	move.w	d2,14(a2)
	move.l	4(a0),d0
	lsl.l	d0
	move.l	d0,d1
	move.l	Pointer(a1),a6
	add.l	20(a6),d1
	move.l	d1,24(a2)
	add.l	(a3),d0
	move.l	d0,20(a2)
	move.l	(a4),d0
	add.l	HorLine(a1),d0
	add.w	(a0),d0
	add.w	(a0),d0
	move.l	d0,16(a2)
	move.l	d0,28(a2)
	move.w	Width(a1),d1
	lsr.w	#3,d1
	sub.w	2(a0),d1
	sub.w	2(a0),d1
	add.w	#2,d1
	sub.w	8(a0),d1
	sub.w	8(a0),d1
	move.w	d1,4(a2)
	move.w	d1,2(a2)
	moveq	#46,d0
	sub.w	2(a0),d0
	sub.w	2(a0),d0
	sub.w	8(a0),d0
	sub.w	8(a0),d0
	move.w	d0,(a2)
	move.w	d0,6(a2)
	move.w	Hight(a1),d0
	lsl.w	#6,d0
	add.w	2(a0),d0
	add.w	8(a0),d0
	move.w	d0,32(a2)
BlitterFinito3	btst	#6,dmaconr(a5)
	bne	BlitterFinito3
	lea	$DFF040,a6
	move.l	(a2)+,bltcmod(a5)
	move.l	(a2)+,bltamod(a5)
	move.l	(a2)+,(a6)+		bltcon0
	move.l	(a2)+,(a6)+		bltafwm
	move.l	(a2)+,(a6)+		bltcpth
	move.l	(a2)+,(a6)+		bltbpth
	move.l	(a2)+,(a6)+		bltapth
	move.l	(a2)+,(a6)+		bltdpth
	move.w	(a2),(a6)		bltsize
	adda.l	#10,a0
	dbf	d6,Abbild
	adda.l	#4,a3
	adda.l	#4,a4
	dbf	d7,Abschnitt
	move.l	Tempo(a1),d0
	add.l	d0,Offset(a1)
	adda.l	#StructLength,a1
	dbf	d5,ObjDraw
	bra	Beweg
BlitterFinito2	btst	#6,dmaconr(a5)
	bne	BlitterFinito2
	move.l	_GfxBase,a1
	move.l	38(a1),cop1lch(a5)
	move.w	#0,copjmp1(a5)
	move.w	#$83E0,dmacon(a5)
	move.l	4.w,a6
	tst.l	ObjPointer1
	beq	NOObjOpened
	move.w	ObjInfo,d6
	sub.w	#1,d6
	bra	KreuzJump
FileNotFound	move.w	ObjInfo,d6
	sub.w	#1,d6
	sub.w	d5,d6
KreuzJump	lea	ObjInfo+2,a5
AllObjs	move.l	Pointer(a5),a3
	moveq	#5,d7
ObjFreeMemer	move.l	(a3)+,a1
	move.l	Length(a5),d0
	jsr	FreeMem(a6)
	dbf	d7,ObjFreeMemer
	adda.l	#StructLength,a5
	dbf	d6,AllObjs
NOObjOpened	move.l	CL,a1
	move.l	#516,d0
	jsr	FreeMem(a6)
	move.l	CL2,a1
	move.l	#516,d0
	jsr	FreeMem(a6)
	lea	BitPlanes1,a3
	moveq	#9,d7
FreeMemer	move.l	(a3)+,a1
	move.l	#9204,d0
	jsr	FreeMem(a6)
	dbf	d7,FreeMemer
	move.l	_DosBase,a1
	jsr	CloseLibrary(a6)
	move.l	_GfxBase,a1
	jsr	CloseLibrary(a6)
	rts
ColorCycle	lea	ColorMap,a0
	adda.l	#18,a0
	move.w	10(a0),d0
	move.l	a0,a1
	adda.l	#10,a1
	adda.l	#12,a0
	moveq	#4,d1
CycleLoop	move.w	-(a1),-(a0)
	dbf	d1,CycleLoop
	move.w	d0,(a0)
	lea	ColorMap+4,a0
	lea	$DFF184,a1
	moveq	#15,d0
CycleLoop2	move.l	(a0)+,(a1)+
	dbf	d0,CycleLoop2
	rts
SideChill	move.l	BitMap2,a0
	move.l	(a0)+,a1
	move.l	(a0)+,a2
	move.l	(a0)+,a3
	move.l	(a0)+,a4
	move.l	(a0),a0
	move.w	#199,d1
	moveq	#0,d0
SideLoop	clr.w	(a0,d0.w)
	clr.w	(a1,d0.w)
	clr.w	(a2,d0.w)
	clr.w	(a3,d0.w)
	clr.w	(a4,d0.w)
	add.w	#46,d0
	dbf	d1,SideLoop
	rts
grafname	GRAFNAME
dosname	DOSNAME
Dateiname1	dc.b	"/pics/Wolke",0
Dateiname2	dc.b	"/pics/Berg",0
Dateiname3	dc.b	"/pics/BackTrees",0
Dateiname4	dc.b	"/pics/Wiese&Baum",0
Dateiname5	dc.b	"/pics/Wiese2",0
Dateiname6	dc.b	"/pics/Wiese1",0
Dateiname7	dc.b	"/pics/Mauer",0
Dateiname8	dc.b	"/pics/Wasser",0
Dateiname9	dc.b	"/pics/Baum",0
Compression	dc.b	0
BitMap1	dc.l	BitPlanes1
BitMap2	dc.l	BitPlanes2
BitPlanes1	ds.l	5
BitPlanes2	ds.l	5
CL	dc.l	0
CL2	dc.l	0
_GfxBase	dc.l	0
_DosBase	dc.l	0
DateiLength	dc.l	0
Empty	dc.l	0
xyPos	dc.l	0
IFFData	dc.l	0
ColorMap	ds.l	16
ObjInfo	dc.w	9
First	dc.l	Dateiname1
ObjLength1	dc.l	0
ObjBegin1	dc.l	ObjPointer1
ObjWidth1	dc.w	0
ObjHight1	dc.w	0
ObjOffset1	dc.l	0
ObjTempo1	dc.l	-2
	dc.l	BlitterInfo1
HorLine1	dc.l	62*46
Second	dc.l	Dateiname2
ObjLength2	dc.l	0
ObjBegin2	dc.l	ObjPointer2
ObjWidth2	dc.w	0
ObjHight2	dc.w	0
ObjOffset2	dc.l	0
ObjTempo2	dc.l	1
	dc.l	BlitterInfo2
HorLine2	dc.l	107*46
Third	dc.l	Dateiname3
ObjLength3	dc.l	0
ObjBegin3	dc.l	ObjPointer3
ObjWidth3	dc.w	0
ObjHight3	dc.w	0
ObjOffset3	dc.l	0
ObjTempo3	dc.l	2
	dc.l	BlitterInfo3
HorLine3	dc.l	102*46
Fourth	dc.l	Dateiname4
ObjLength4	dc.l	0
ObjBegin4	dc.l	ObjPointer4
ObjWidth4	dc.w	0
ObjHight4	dc.w	0
ObjOffset4	dc.l	0
ObjTempo4	dc.l	4
	dc.l	BlitterInfo4
HorLine4	dc.l	80*46
Fifth	dc.l	Dateiname5
ObjLength5	dc.l	0
ObjBegin5	dc.l	ObjPointer5
ObjWidth5	dc.w	0
ObjHight5	dc.w	0
ObjOffset5	dc.l	0
ObjTempo5	dc.l	6
	dc.l	BlitterInfo5
HorLine5	dc.l	124*46
Sixth	dc.l	Dateiname6
ObjLength6	dc.l	0
ObjBegin6	dc.l	ObjPointer6
ObjWidth6	dc.w	0
ObjHight6	dc.w	0
ObjOffset6	dc.l	0
ObjTempo6	dc.l	8
	dc.l	BlitterInfo6
HorLine6	dc.l	137*46
Seventh	dc.l	Dateiname7
ObjLength7	dc.l	0
ObjBegin7	dc.l	ObjPointer7
ObjWidth7	dc.w	0
ObjHight7	dc.w	0
ObjOffset7	dc.l	0
ObjTempo7	dc.l	10
	dc.l	BlitterInfo7
HorLine7	dc.l	128*46
Eighth	dc.l	Dateiname8
ObjLength8	dc.l	0
ObjBegin8	dc.l	ObjPointer8
ObjWidth8	dc.w	0
ObjHight8	dc.w	0
ObjOffset8	dc.l	0
ObjTempo8	dc.l	2
	dc.l	BlitterInfo8
HorLine8	dc.l	175*46
Ninth	dc.l	Dateiname9
ObjLength9	dc.l	0
ObjBegin9	dc.l	ObjPointer9
ObjWidth9	dc.w	0
ObjHight9	dc.w	0
ObjOffset9	dc.l	0
ObjTempo9	dc.l	13
	dc.l	BlitterInfo9
HorLine9	dc.l	0*46
BlitterInfo1	dc.w	0
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
BlitterInfo2	dc.w	0
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
BlitterInfo3	dc.w	0
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
BlitterInfo4	dc.w	0
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
BlitterInfo5	dc.w	0
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
BlitterInfo6	dc.w	0
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
BlitterInfo7	dc.w	0
	ds.w	5
	ds.w	5
BlitterInfo8	dc.w	0
	ds.w	5
	ds.w	5
	ds.w	5
	ds.w	5
BlitterInfo9	dc.w	0
	ds.w	5
	ds.w	5
BlitSpeicher	ds.w	17
ObjPointer1	ds.l	5
ObjShadow1	dc.l	0
ObjPointer2	ds.l	5
ObjShadow2	dc.l	0
ObjPointer3	ds.l	5
ObjShadow3	dc.l	0
ObjPointer4	ds.l	5
ObjShadow4	dc.l	0
ObjPointer5	ds.l	5
ObjShadow5	dc.l	0
ObjPointer6	ds.l	5
ObjShadow6	dc.l	0
ObjPointer7	ds.l	5
ObjShadow7	dc.l	0
ObjPointer8	ds.l	5
ObjShadow8	dc.l	0
ObjPointer9	ds.l	5
ObjShadow9	dc.l	0
BytesRowLong	dc.w	0
BytesPerRow	dc.w	0
Byte	dc.w	0
Breite	dc.w	0
Hohe	dc.w	0
Depth	dc.w	0
Color01Smoove	dc.w	$39C
	dc.w	$38C
	dc.w	$38B
	dc.w	$48B
	dc.w	$47B
	dc.w	$47B
	dc.w	$48B
	dc.w	$48B
	dc.w	$58B
	dc.w	$58B
	dc.w	$68B
	dc.w	$67B
	dc.w	$67A
	dc.w	$68A
	dc.w	$58A
	dc.w	$59A
	dc.w	$59B
	dc.w	$69B
	dc.w	$6AB
	dc.w	$6BB
	dc.w	$7BC
	dc.w	$7CC
	dc.w	$6CC
	dc.w	$6DC
	dc.w	$6DD
	dc.w	$6CD
	dc.w	$7CD
	dc.w	$7CE
	dc.w	$8CE
	dc.w	$8BE
	dc.w	$8BF
	dc.w	$8CF
	dc.w	$9CF
	dc.w	$ACF
	dc.w	$ADF
	dc.w	$BDF
	dc.w	$BEF
	dc.w	$CEF
	dc.w	$CEE
	dc.w	$BEE
	dc.w	$BED
	dc.w	$BDD
	dc.w	$ADD
	dc.w	$ACD
	dc.w	$9CD
	dc.w	$9CC
	dc.w	$9BC
	dc.w	$8BC
	dc.w	$8AC
Sprung	dc.w	$BDC
	dc.w	$ADC
	dc.w	$ACC
	dc.w	$9CC
	dc.w	$9BC
	dc.w	$8BC
	dc.w	$8AC
	dc.w	$7AC
	dc.w	$79C
	dc.w	$69C
